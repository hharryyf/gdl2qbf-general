;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; author: Richard Emslie
;; game: 3x3 traffic-lights
;; desc: https://boardgamegeek.com/boardgame/1893/traffic-lights
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(role xplayer)
(role oplayer)


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Base & Input
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(index 1) (index 2) (index 3)
(<= (base (cell ?x ?y blank)) (index ?x) (index ?y))
(<= (base (cell ?x ?y red)) (index ?x) (index ?y))
(<= (base (cell ?x ?y yellow)) (index ?x) (index ?y))
(<= (base (cell ?x ?y green)) (index ?x) (index ?y))
(<= (base (control ?p)) (role ?p))

(<= (input ?p (mark ?x ?y red)) (index ?x) (index ?y) (role ?p))
(<= (input ?p (mark ?x ?y yellow)) (index ?x) (index ?y) (role ?p))
(<= (input ?p (mark ?x ?y green)) (index ?x) (index ?y) (role ?p))
(<= (input ?p noop) (role ?p))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Initial State
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(init (cell 1 1 blank))
(init (cell 1 2 blank))
(init (cell 1 3 blank))
(init (cell 2 1 blank))
(init (cell 2 2 blank))
(init (cell 2 3 blank))
(init (cell 3 1 blank))
(init (cell 3 2 blank))
(init (cell 3 3 blank))
(init (control xplayer))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Transitions
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (next (cell ?m ?n red))
(does ?p (mark ?m ?n red))
(true (cell ?m ?n blank)))

(<= (next (cell ?m ?n yellow))
(does ?p (mark ?m ?n yellow))
(true (cell ?m ?n red)))

(<= (next (cell ?m ?n green))
(does ?p (mark ?m ?n green))
(true (cell ?m ?n yellow)))

(<= (next (cell ?m ?n ?c0))
(does ?p (mark ?j ?k ?c1))
(true (cell ?m ?n ?c0))
(distinct ?m ?j))


(<= (next (cell ?m ?n ?c0))
(does ?p (mark ?j ?k ?c1))
(true (cell ?m ?n ?c0))
(distinct ?n ?k))


(<= (next (control xplayer))
(true (control oplayer)))

(<= (next (control oplayer))
(true (control xplayer)))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Legal moves
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (legal ?w (mark ?x ?y red))
(true (cell ?x ?y blank))
(true (control ?w)))

(<= (legal ?w (mark ?x ?y yellow))
(true (cell ?x ?y red))
(true (control ?w)))

(<= (legal ?w (mark ?x ?y green))
(true (cell ?x ?y yellow))
(true (control ?w)))

(<= (legal xplayer noop)
(true (control oplayer)))

(<= (legal oplayer noop)
(true (control xplayer)))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; terminals
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= terminal
aline)

(<= terminal
(not open))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; goals
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (goal xplayer 100)
aline
(true (control oplayer))
)

(<= (goal xplayer 50)
(not aline)
(not open))

(<= (goal xplayer 0)
aline
(true (control xplayer))
)

(<= (goal oplayer 100)
aline
(true (control xplayer))
)

(<= (goal oplayer 50)
(not aline)
(not open))

(<= (goal oplayer 0)
aline
(true (control oplayer))
)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; helpers
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (row ?m ?x)
(true (cell ?m 1 ?x))
(true (cell ?m 2 ?x))
(true (cell ?m 3 ?x)))

(<= (column ?n ?x)
(true (cell 1 ?n ?x))
(true (cell 2 ?n ?x))
(true (cell 3 ?n ?x)))

(<= (diagonal ?x)
(true (cell 1 1 ?x))
(true (cell 2 2 ?x))
(true (cell 3 3 ?x)))

(<= (diagonal ?x)
(true (cell 1 3 ?x))
(true (cell 2 2 ?x))
(true (cell 3 1 ?x)))

(<= (line ?x) (row ?m ?x))
(<= (line ?x) (column ?m ?x))
(<= (line ?x) (diagonal ?x))

(<= aline (line red))
(<= aline (line yellow))
(<= aline (line green))

(<= open
(true (cell ?m ?n blank)))

(<= open
(true (cell ?m ?n red)))

(<= open
(true (cell ?m ?n yellow)))
