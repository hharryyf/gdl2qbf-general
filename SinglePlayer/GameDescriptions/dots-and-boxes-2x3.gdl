; Dots and Boxes
; by Alex Landau, 7/10/11 (alandau@cs.stanford.edu)
; 2x3 boxes, 3x4 dots

; The search space is large, but strategy is rather tricky until the
; end of the game anyway. Heuristics may help.

(role xplayer)
(role oplayer)

(xindex 1)
(xindex 2)
(xindex 3)
(xindex 4)
(yindex 1)
(yindex 2)
(yindex 3)
(next_index_x 1 2)
(next_index_x 2 3)
(next_index_x 3 4)
(next_index_y 1 2)
(next_index_y 2 3)

(init (box_count xplayer 0))
(init (box_count oplayer 0))

(init (control xplayer))

(<= (undrawn ?x ?y1 ?x ?y2)
	(xindex ?x)
	(next_index_y ?y1 ?y2)
	(not (true (line ?x ?y1 ?x ?y2))))
(<= (undrawn ?x1 ?y ?x2 ?y)
	(next_index_x ?x1 ?x2)
	(yindex ?y)
	(not (true (line ?x1 ?y ?x2 ?y))))
(<= any_undrawn
	(undrawn ?x1 ?y1 ?x2 ?y2))

(<= (legal ?player (draw ?x1 ?y1 ?x2 ?y2))
	(true (control ?player))
	(undrawn ?x1 ?y1 ?x2 ?y2))
	
(<= (next (line ?x1 ?y1 ?x2 ?y2))
	(does ?player (draw ?x1 ?y1 ?x2 ?y2)))
(<= (next (line ?x1 ?y1 ?x2 ?y2))
	(true (line ?x1 ?y1 ?x2 ?y2)))

(<= (legal ?player noop)
	(role ?player)
	(not (true (control ?player))))

; Box-drawing mechanics
; Look at the line we just drew
(<= (v_drawn ?x ?y1 ?x ?y2)
	(does ?player (draw ?x ?y1 ?x ?y2)))
(<= (h_drawn ?x1 ?y ?x2 ?y)
	(does ?player (draw ?x1 ?y ?x2 ?y)))
; Look on either side of the line to see if there's a box
; Record the location of the box for visualization purposes
(<= (box_formed ?x1 ?y1)
	(v_drawn ?x1 ?y1 ?x1 ?y2)
	(true (line ?x1 ?y2 ?x2 ?y2))
	(true (line ?x1 ?y1 ?x2 ?y1))
	(true (line ?x2 ?y1 ?x2 ?y2)))
(<= (box_formed ?x1 ?y1)
	(v_drawn ?x2 ?y1 ?x2 ?y2)
	(true (line ?x1 ?y1 ?x2 ?y1))
	(true (line ?x1 ?y1 ?x1 ?y2))
	(true (line ?x1 ?y2 ?x2 ?y2)))
(<= (box_formed ?x1 ?y1)
	(h_drawn ?x1 ?y1 ?x2 ?y1)
	(true (line ?x2 ?y1 ?x2 ?y2))
	(true (line ?x1 ?y1 ?x1 ?y2))
	(true (line ?x1 ?y2 ?x2 ?y2)))
(<= (box_formed ?x1 ?y1)
	(h_drawn ?x1 ?y2 ?x2 ?y2)
	(true (line ?x1 ?y1 ?x1 ?y2))
	(true (line ?x1 ?y1 ?x2 ?y1))
	(true (line ?x2 ?y1 ?x2 ?y2)))
(<= any_box_formed
	(box_formed ?x ?y))
(<= two_boxes_formed
	(box_formed ?x1 ?y1)
	(box_formed ?x2 ?y2)
	(distinct ?x1 ?x2))

(<= two_boxes_formed
	(box_formed ?x1 ?y1)
	(box_formed ?x2 ?y2)
	(distinct ?y1 ?y2))

; Updating the players' scores
(<= (next (box_count ?player ?n))
	(true (box_count ?player ?n))
	(not (true (control ?player))))
(<= (next (box_count ?player ?n))
	(true (box_count ?player ?n))
	(true (control ?player))
	(not any_box_formed))
(<= (next (box_count ?player ?np1))
	(true (box_count ?player ?n))
	(true (control ?player))
	any_box_formed
	(not two_boxes_formed)
	(succ ?n ?np1))
(<= (next (box_count ?player ?np2))
	(true (box_count ?player ?n))
	(true (control ?player))
	two_boxes_formed
	(succ ?n ?np1)
	(succ ?np1 ?np2))
	
; If you formed a box, take another turn
(<= (next (control ?player))
	(true (control ?player))
	any_box_formed)
(<= (next (control xplayer))
	(true (control oplayer))
	(not any_box_formed))
(<= (next (control oplayer))
	(true (control xplayer))
	(not any_box_formed))

; These parts of the state are purely for visualization purposes.
(<= (next (box ?x ?y x))
	(box_formed ?x ?y)
	(true (control xplayer)))
(<= (next (box ?x ?y o))
	(box_formed ?x ?y)
	(true (control oplayer)))
(<= (next (box ?x ?y ?mark))
	(true (box ?x ?y ?mark)))

; The game ends when there are no more lines to draw
(<= terminal
	(not any_undrawn))

; The higher score wins
(<= x_wins
	(true (box_count xplayer ?xscore))
	(true (box_count oplayer ?oscore))
	(gt ?xscore ?oscore))
(<= o_wins
	(true (box_count xplayer ?xscore))
	(true (box_count oplayer ?oscore))
	(gt ?oscore ?xscore))
; This breaks the tie if the board count is even
; (possible on a board with an even number of boxes)
(<= o_wins
	(true (box_count xplayer ?tied))
	(true (box_count oplayer ?tied)))
(<= (goal xplayer 100)
	x_wins)
(<= (goal xplayer 0)
	(not x_wins))
(<= (goal oplayer 100)
	o_wins)
(<= (goal oplayer 0)
	(not o_wins))

; gt is for the score only; based on succ
(<= (gt ?h ?l)
	(succ ?l ?h))
(<= (gt ?h ?l)
	(succ ?m ?h)
	(gt ?m ?l)
	(succ ?l ?lp1))
; succ is for the score only; there are 6 boxes
(succ 0 1)
(succ 1 2)
(succ 2 3)
(succ 3 4)
(succ 4 5)
(succ 5 6)
